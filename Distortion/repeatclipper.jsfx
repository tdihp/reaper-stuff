desc:RepeatClipper
author: tdihp
version: 0.1
about:
  This fx takes the idea of clipper to a different direction, instead of keeping
  a flat line, this fx repeat or mirror the signal back to its dynamic range,
  making unpleasant distortive waveform out of the original as soon as it
  crosses threshold.

/*
Copyright (c) 2025 Ping He

Permission is hereby granted, free of charge, to any person obtaining a copy
of this software and associated documentation files (the "Software"), to deal
in the Software without restriction, including without limitation the rights
to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
copies of the Software, and to permit persons to whom the Software is
furnished to do so, subject to the following conditions:

The above copyright notice and this permission notice shall be included in all
copies or substantial portions of the Software.

THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
SOFTWARE.
*/

options:gfx_idle
slider1:threshold_db=0<-50,0,0.1>Ceiling DB of clipping
slider2:repeat_percentage=5<0,200,0.01>Repeating % of ceiling
slider3:reflect=0<0,1,1>Reflection (mild)
slider4:logscale=0<-5,5,0.01>log(Scale)
slider5:gain_db=-2<-15,0,0.1>Gain DB
in_pin:left input
in_pin:right input
out_pin:left output
out_pin:right output

@init
function repeater(s) local(x) (
  x = abs(s) - threshold_abs;
  (x > 0)? (
    (repeat_size > 0)? (
      x = x * scale / repeat_size;
      floor_x = floor(x);
      x -= floor_x;
      ((floor_x % 2 - 1) && reflect)? (
        x = 1 - x;
      );
      x *= repeat_size;
      x += repeat_abs;
      sign(s) * (x);
    ) : (
      x = threshold_abs;
      sign(s) * threshold_abs;
    );
  ) : s;
);

@slider
threshold_abs = 10^(threshold_db*.05);
amp = 10^((-threshold_db + gain_db)*.05);
repeat_abs = threshold_abs * (1 - repeat_percentage * .01);
repeat_size = threshold_abs * repeat_percentage * .01;
scale = $e^logscale;

@gfx 256 256
gfx_clear = 0;
x_0 = floor(gfx_w / 2);
y_0 = floor(gfx_h / 2);
gfx_x = x_0; gfx_y = y_0;
//gfx_setpixel(1,1,1);
gfx_set(1);
gfx_line(0,y_0,gfx_w-1,y_0, 0);
gfx_line(x_0,0,x_0, gfx_h-1, 0);
gfx_set(0, 1, 0);
gfx_line(0,gfx_h-1,gfx_w-1,0, 0);
px = 0;
while (px < gfx_w) (
  x = (px - x_0) * .5 / gfx_w ;
  gfx_x = px;
  gfx_y =  floor(gfx_h * 2 * (-repeater(x) + 0.5)) - y_0;
  gfx_setpixel(1,1,0);
  px += 1;
);

@sample
spl0 = repeater(spl0) * amp;
spl1 = repeater(spl1) * amp;
